<html>

<head>
    <title>OrbitalJS - CS32310 Assignment - Connor Luke Goddard</title>
    <meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

    <script type="text/javascript" src="js/external/jquery/jquery.js"></script>
    <script type="text/javascript" src="js/jquery-ui.min.js"></script>
    <script type="text/javascript" src="js/gl-matrix-2.2.1.min.js"></script>
    <script type="text/javascript" src="js/webgl-utils.js"></script>

    <script type="text/javascript" src="js/config.js"></script>
    <script type="text/javascript" src="js/utils.js"></script>
    <script type="text/javascript" src="js/orbital.js"></script>
    <script type="text/javascript" src="js/texture.js"></script>
    <script type="text/javascript" src="js/event.js"></script>
    <script type="text/javascript" src="js/camera.js"></script>
    <script type="text/javascript" src="js/scene.js"></script>
    <script type="text/javascript" src="js/app.js"></script>

    <!-- Main WebGL fragment shader. Supports Phong lighting model (ambient, diffuse and specular terms) plus attentuation and multi-texturing (including animated textures). -->
    <script id="shader-fs" type="x-shader/x-fragment">

        //This fragment shader code has been modified from original code available at: http://learningwebgl.com/blog/?p=370

        precision mediump float;

        varying vec2 vTextureCoord1;
        varying vec2 vTextureCoord2;
        varying vec3 vTransformedNormal;
        varying vec4 vPosition;

        uniform float uMaterialShininess;

        uniform float uAttenuation;

        uniform bool uUseLighting;
        uniform bool uUseMultiTextures;

        uniform vec3 uAmbientColor;

        uniform vec3 uPointLightingLocation;
        uniform vec3 uPointLightingSpecularColor;
        uniform vec3 uPointLightingDiffuseColor;

        uniform sampler2D uSampler1;
        uniform sampler2D uSampler2;
        uniform sampler2D uSampler3;

        uniform float uAlpha;

        void main(void) {

            vec3 lightWeighting;

            vec3 lightDirection = normalize(uPointLightingLocation.xyz - vPosition.xyz);
            vec3 normal = normalize(vTransformedNormal);

            vec4 planetTexture = texture2D(uSampler1, vec2(vTextureCoord1.s, vTextureCoord1.t));
            vec4 earthNightTexture = texture2D(uSampler3, vec2(vTextureCoord1.s, vTextureCoord1.t));
            vec4 earthCloudTexture = texture2D(uSampler2, vec2(vTextureCoord2.s, vTextureCoord2.t));

            //Calculate the attenuation factor using constant, linear and quadratric constants.
            //Based on original code from "Beginning WebGL for HTML5", B. Danchilla, Apress Publishing, 2012.

            float d = length(uPointLightingLocation.xyz - vPosition.xyz);
            float attenuation = min((1.0 / (0.0001 + (0.0001 * d) + (uAttenuation * (d * d)))), 1.0);

            //Increase diffuse power to make the difference between day and night more noticable.
            float diffusePower = 1.0;

            vec3 eyeDirection = normalize(-vPosition.xyz);
            vec3 reflectionDirection = reflect(-lightDirection, normal);

            //Calculate the specular light term weighting taking into account the material "shininess".
            float specularLightWeighting = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uMaterialShininess);

            //Calculate the diffuse light term weighting.
            float diffuseLightWeighting = max(dot(normalize(normal), lightDirection), 0.0);
            diffuseLightWeighting = clamp(diffuseLightWeighting * diffusePower, -1.0, 1.0);

            //linearly interpolate between the two day and night textures for the Earth.
            vec3 earthTextureColor = mix(earthNightTexture.rgb, planetTexture.rgb, diffuseLightWeighting);


            //Check if we wish to use the Phong lighting model..
            if (!uUseLighting) {

                //If not, simply set an ambient term to full brightness.
                lightWeighting = vec3(1.0, 1.0, 1.0);

            } else {

                //Otherwise, calculate the lighting conditions for the given fragment, taking into account the three terms of the Phong lighting model and the light attentuation.
                lightWeighting = ((uAmbientColor * attenuation) + ((uPointLightingSpecularColor * attenuation) * specularLightWeighting) + ((uPointLightingDiffuseColor * attenuation) * diffuseLightWeighting));

            }

            //Check if we are applying multiple textures to an object.. (in this case only the Earth)
            if (uUseMultiTextures) {

                //If so apply the multiple textures (The Earth day/night combination calculated earlier and the cloud texture)
                gl_FragColor = vec4((earthTextureColor + (earthCloudTexture.rgb * earthCloudTexture.a)) * lightWeighting, planetTexture.a);

            } else {

                //Otherwise only apply the single texture.
                gl_FragColor = vec4(planetTexture.rgb * lightWeighting, planetTexture.a);

            }
        }
    </script>

    <!-- Dedicated fragment shader for the scene 'sky-box'. Only uses a very simple lighting model (ambient term only) -->
    <script id="shader-fs-skybox" type="x-shader/x-fragment">

        precision mediump float;

        varying vec2 vTextureCoord1;

        uniform sampler2D uSampler1;

        void main(void) {

            //Apply the single texture using only an ambient lighting term.

            vec4 fragmentColor1 = texture2D(uSampler1, vec2(vTextureCoord1.s, vTextureCoord1.t));

            gl_FragColor = vec4(fragmentColor1.rgb, fragmentColor1.a);


        }
    </script>

    <!-- WebGL vertex shader. Used by both fragment shaders. -->
    <script id="shader-vs" type="x-shader/x-vertex">

        //This vertex shader code has been modified from original code available at: http://learningwebgl.com/blog/?p=370

        attribute vec3 aVertexPosition;
        attribute vec3 aVertexNormal;
        attribute vec2 aTextureCoord1;

        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;
        uniform mat3 uNMatrix;
        uniform mat4 uTMatrix;

        varying vec2 vTextureCoord1;
        varying vec2 vTextureCoord2;
        varying vec3 vTransformedNormal;
        varying vec4 vPosition;

        void main(void) {

            //Calculate current vertex position and pass to fragment shader.
            vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);

            //Set current position to draw.
            gl_Position = uPMatrix * vPosition;

            //Set main texture coordinates.
            vTextureCoord1 = aTextureCoord1;

            //Set secondary texture coordinates applying any transforms from the texture transform matrix (used to rotate clouds around the Earth).
            vTextureCoord2 = (uTMatrix * vec4(aTextureCoord1, 0.0, 1.0)).xy;

            //Calculate the surface normal.
            vTransformedNormal = uNMatrix * aVertexNormal;
        }
    </script>

    <script src="js/app-start.js"></script>
    <link rel="stylesheet" type="text/css" href="css/jquery-ui.min.css">
    <link rel="stylesheet" type="text/css" href="css/jquery-ui.structure.min.css">
    <link rel="stylesheet" type="text/css" href="css/jquery-ui.theme.min.css">
    <link rel="stylesheet" type="text/css" href="css/style.css">
</head>


<body onload="appStart();">

    <div id="header">
        <div id="header-container">
            <div id="title">Orbital<span class="bold">JS</span></div>
            <div id="controls">

                <div class="control-container">
                    <h2>Lighting</h2>

                    <div class="slider-container">
                        <p>Ambient Lighting</p>
                        <div id="ambient-slider"></div>
                    </div>

                    <div class="slider-container">
                        <p>Diffuse Lighting</p>
                        <div id="diffuse-slider"></div>
                    </div>

                    <div class="slider-container">
                        <p>Specular Lighting</p>
                        <div id="specular-slider"></div>
                    </div>

                    <div class="slider-container">
                        <p>Attenuation</p>
                        <div id="attenuation-slider"></div>
                    </div>

                    <a id="toggle-lighting" class="button">Disable Lighting</a>
                </div>

                <div class="control-container">
                    <h2>Orbits</h2>

                    <div id="eccentricity-control" class="slider-container">
                        <p>Eccentricity</p>
                        <div id="eccentricity-slider"></div>
                    </div>

                    <a id="toggle-orbit" class="button">Circular</a>

                </div>

                <div class="control-container">
                    <h2>Animation</h2>

                    <div class="slider-container">
                        <p>Direction</p>
                        <div id="direction-slider"></div>
                    </div>

                    <div class="slider-container">
                        <p>Speed</p>
                        <div id="velocity-slider"></div>
                    </div>

                    <a id="toggle-animation" class="button">Pause</a>
                </div>
            </div>
        </div>
    </div>

    <canvas id="app-canvas" style="border: none;" width="500" height="500"></canvas>

    <div id="footer">&copy; Connor Luke Goddard 2014. All rights reserved.</div>

</body>

</html>
